import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

ext {
  componentName = 'Interlok AWS/Kinesis'
  componentDesc = "Components that interact AWS Kinesis data streams"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  awsSDKVersion = "1.12.195"
  jacksonVersion = "2.13.2.20220328"
  nettyVersion = "4.1.75.Final"
  kotlinVersion = "1.4.32"
  wireVersion = "4.0.1"
}
dependencies {
  implementation ("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion")
  
  api ("com.amazonaws:aws-java-sdk-kinesis:$awsSDKVersion")
  api ("com.amazonaws:amazon-kinesis-client:1.14.8") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-cbor"
    exclude group: "com.google.protobuf", module: "protobuf-java"
  }
  api ("com.amazonaws:aws-java-sdk-core:$awsSDKVersion") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
    exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-cbor"
    exclude group: "org.apache.avro"
  }
  api ("commons-codec:commons-codec:1.15")
  // 0.13.0 doesn't contain the windows execuatble (bad?) so use the older version for now.
  // 0.13.1 does containthe windows binaries
  // 0.14 does not contain the windows binary.
  api ("com.amazonaws:amazon-kinesis-producer:0.14.12") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.google.protobuf", module: "protobuf-java"
    // Exclude netty so we can pin the version.
    exclude group: "io.netty"
    exclude group: "org.apache.avro"
  }

  implementation ("org.apache.avro:avro:1.11.0") {
    exclude group: "org.apache.commons", module: "commons-compress"
  }
  
  implementation ("org.apache.commons:commons-compress:1.21")

  api ("com.google.guava:guava:31.1-jre")
  implementation ("io.netty:netty-buffer:$nettyVersion")
  implementation ("io.netty:netty-common:$nettyVersion")
  implementation ("io.netty:netty-handler:$nettyVersion")
  implementation ("io.netty:netty-transport:$nettyVersion")
  implementation ("io.netty:netty-transport-native-epoll:$nettyVersion")
  implementation ("io.netty:netty-transport-native-unix-common:$nettyVersion")
  implementation ("io.netty:netty-transport-native-kqueue:$nettyVersion")
  implementation ("io.netty:netty-codec:$nettyVersion")
  implementation ("io.netty:netty-codec-http:$nettyVersion")
    
  implementation ("com.squareup.wire:wire-schema:$wireVersion")
  implementation ("com.squareup.wire:wire-compiler:$wireVersion")

  api ("com.google.protobuf:protobuf-java:3.19.4")
  // Dependency to platform/BOM
  api (platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
  api ("com.fasterxml.jackson.core:jackson-databind")
  api ("com.fasterxml.jackson.core:jackson-core")
  api ("com.fasterxml.jackson.core:jackson-annotations")
  // It will probably march in lockstep, but since it's a different
  // group I will make it separate.
  api ("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor")
  api project(':interlok-aws-common')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", componentDesc)
			asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.9.1+")
            properties.appendNode("tags", "aws,kinesis")
            properties.appendNode("license", "false")
            properties.appendNode("readme", "https://github.com/adaptris/interlok-aws/raw/develop/README.md")
            properties.appendNode("repository", "https://github.com/adaptris/interlok-aws")
       }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete file(testResourcesDir() + "/unit-tests.properties"), file(testResourcesDir() + "/unit-tests.properties.resolved"), delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
processTestResources.dependsOn copyUnitTestProperties
