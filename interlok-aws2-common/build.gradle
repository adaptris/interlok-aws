ext {
  componentName="Interlok AWS2/Common"
  componentDesc="Common components for accessing AWS"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  awsSDKVersion = "2.21.6"
  jacksonVersion = "2.17.2"
}

dependencies {
  api ("software.amazon.awssdk:sdk-core:$awsSDKVersion") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
    exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-cbor"
  }
  api ("software.amazon.awssdk:apache-client:$awsSDKVersion") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
    exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-cbor"
  }
  api ("software.amazon.awssdk:sts:$awsSDKVersion") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
    exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-cbor"
  }
  api ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
  api ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
  api ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
  // It will probably march in lockstep, but since it's a different
  // group I will make it separate.
  api ("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion")
  api ("com.adaptris:interlok-apache-http:$interlokCoreVersion") { changing = true}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}
artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", componentDesc)
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.3.0+")
            properties.appendNode("tags", "aws")
            properties.appendNode("license", "false")
            properties.appendNode("readme", "https://github.com/adaptris/interlok-aws/raw/develop/README.md")
            properties.appendNode("repository", "https://github.com/adaptris/interlok-aws")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
