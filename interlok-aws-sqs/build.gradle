ext {
  componentName='Interlok AWS/SQS'
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

// In this section you declare the dependencies for your production and test code
dependencies {
  compile ("com.amazonaws:aws-java-sdk-core:$awsSDKVersion") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
  }
  compile ("com.amazonaws:aws-java-sdk-sqs:$awsSDKVersion")
  compile ("com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8")
  compile ("com.fasterxml.jackson.core:jackson-databind:2.11.3")
  compile ("org.apache.httpcomponents:httpclient:4.5.13")
  compile ("commons-codec:commons-codec:1.15")
  compile project(':interlok-aws-common')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}
artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("description", "Publishing & Consuming from AWS S3")
            asNode().appendNode("name", componentName)
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.3.0+")
            properties.appendNode("tags", "aws,sqs")
            properties.appendNode("license", "false")
            properties.appendNode("readme", "https://github.com/adaptris/interlok-aws/raw/develop/README.md")
            properties.appendNode("repository", "https://github.com/adaptris/interlok-aws")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete file(testResourcesDir() + "/unit-tests.properties"), file(testResourcesDir() + "/unit-tests.properties.resolved"), delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
processTestResources.dependsOn copyUnitTestProperties
